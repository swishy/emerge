defmodule EmergeSkia.BuildConfig do
  @moduledoc false

  @version Mix.Project.config()[:version]
  @force_precompiled_build_env_key "EMERGE_SKIA_BUILD"
  @load_macos_nif_env_key "EMERGE_SKIA_LOAD_MACOS_NIF"
  @build_local_macos_host_env_key "EMERGE_SKIA_MACOS_HOST_BUILD_LOCAL"
  @macos_host_cache_dir_env_key "EMERGE_SKIA_MACOS_HOST_CACHE_DIR"
  @checksum_only_env_key "EMERGE_SKIA_CHECKSUM_ONLY"
  @github_token_env_key "EMERGE_SKIA_GITHUB_TOKEN"
  @precompiled_source_url_env_key "EMERGE_SKIA_PRECOMPILED_SOURCE_URL"
  @precompiled_targets ["x86_64-unknown-linux-gnu", "aarch64-unknown-linux-gnu"]
  @macos_host_targets ["aarch64-apple-darwin", "x86_64-apple-darwin"]
  @precompiled_nif_versions ["2.15"]
  @valid_backends [:wayland, :drm, :macos, :ios, :android, :fbdev]
  @default_precompiled_source_url Mix.Project.config()[:source_url]

  @default_compiled_backends (
                               env = System.get_env()

                               cc_prefix =
                                 env
                                 |> Map.get("CC")
                                 |> case do
                                   nil ->
                                     nil

                                   compiler ->
                                     compiler
                                     |> String.split(~r/\s+/, trim: true)
                                     |> List.first()
                                     |> case do
                                       nil ->
                                         nil

                                       path ->
                                         path
                                         |> Path.basename()
                                         |> String.split("-")
                                         |> Enum.drop(-1)
                                         |> Enum.join("-")
                                     end
                                 end

                               has_target_env? =
                                 case {Map.get(env, "TARGET_ARCH"), Map.get(env, "TARGET_OS")} do
                                   {arch, os}
                                   when is_binary(arch) and arch != "" and is_binary(os) and
                                          os != "" ->
                                     true

                                   _ ->
                                     false
                                 end

                               has_mix_target? =
                                 case Map.get(env, "MIX_TARGET") do
                                   target when is_binary(target) and target not in ["", "host"] ->
                                     true

                                   _ ->
                                     false
                                 end

                               if Map.get(env, "NERVES_SDK_SYSROOT") not in [nil, ""] or
                                    has_mix_target? or
                                    cc_prefix in [
                                      "armv6-nerves-linux-gnueabihf",
                                      "armv7-nerves-linux-gnueabihf",
                                      "aarch64-nerves-linux-gnu",
                                      "x86_64-nerves-linux-musl"
                                    ] or has_target_env? do
                                 [:drm]
                               else
                                 case Map.get(env, "TARGET_OS") do
                                   os when is_binary(os) and os != "" ->
                                  cond do
                                    os == "darwin" -> [:macos]
                                    os == "ios" -> [:ios]
                                    os == "android" -> [:android]
                                    true -> [:wayland]
                                  end

                                   _ ->
                                     if :os.type() == {:unix, :darwin},
                                       do: [:macos],
                                       else: [:wayland]
                                 end
                               end
                             )

  @compiled_backends (
                       backends =
                         Application.compile_env(
                           :emerge,
                           :compiled_backends,
                           @default_compiled_backends
                         )

                       invalid_entries =
                         if is_list(backends) do
                           Enum.reject(backends, &(&1 in @valid_backends))
                         else
                           []
                         end

                       cond do
                         not is_list(backends) ->
                           raise ArgumentError,
                                 "config :emerge, compiled_backends: ... must be a list of backend atoms, got: #{inspect(backends)}"

                         invalid_entries != [] ->
                           raise ArgumentError,
"config :emerge, compiled_backends: ... must be a list containing only :wayland, :drm, :macos, :ios, :android, and :fbdev, got invalid entries: #{inspect(invalid_entries)}"

                          true ->
                            for backend <- @valid_backends, backend in backends, do: backend
                       end
                     )

  @default_runtime_backend Enum.find(@valid_backends, &(&1 in @compiled_backends)) || :wayland

  @doc false
  def default_compiled_backends, do: @default_compiled_backends

  @doc false
  def compiled_backends, do: @compiled_backends

  @doc false
  def default_runtime_backend, do: @default_runtime_backend

  @doc false
  def force_precompiled_build_env_key, do: @force_precompiled_build_env_key

  @doc false
  def load_macos_nif_env_key, do: @load_macos_nif_env_key

  @doc false
  def build_local_macos_host_env_key, do: @build_local_macos_host_env_key

  @doc false
  def macos_host_cache_dir_env_key, do: @macos_host_cache_dir_env_key

  @doc false
  def checksum_only_env_key, do: @checksum_only_env_key

  @doc false
  def precompiled_targets, do: @precompiled_targets

  @doc false
  def macos_host_targets, do: @macos_host_targets

  @doc false
  def precompiled_nif_versions, do: @precompiled_nif_versions

  @doc false
  def github_token_env_key, do: @github_token_env_key

  @doc false
  def precompiled_source_url_env_key, do: @precompiled_source_url_env_key

  @doc false
  def checksum_only_mode?(env \\ System.get_env()) when is_map(env) do
    Map.get(env, @checksum_only_env_key) in ["1", "true"]
  end

  @doc false
  def default_compiled_backends(env) when is_map(env) do
    cond do
      nerves_build_env?(env) -> [:drm]
      host_android?(env) -> [:android]
      host_ios?(env) -> [:ios]
      host_darwin?(env) -> [:macos]
      true -> [:wayland]
    end
  end

  defp host_android?(env) when is_map(env) do
    Map.get(env, "TARGET_OS") == "android"
  end

  defp host_ios?(env) when is_map(env) do
    Map.get(env, "TARGET_OS") == "ios"
  end

  @doc false
  def nerves_build_env?(env) when is_map(env) do
    value_present?(Map.get(env, "NERVES_SDK_SYSROOT")) ||
      mix_target?(env) ||
      nerves_compiler?(Map.get(env, "CC"))
  end

  @doc false
  def normalize_compiled_backends!(backends) when is_list(backends) do
    invalid_entries = Enum.reject(backends, &(&1 in @valid_backends))

    if invalid_entries != [] do
      raise ArgumentError,
"config :emerge, compiled_backends: ... must be a list containing only :wayland, :drm, :macos, :ios, :android, and :fbdev, got invalid entries: #{inspect(invalid_entries)}"
    end

    for backend <- @valid_backends, backend in backends, do: backend
  end

  def normalize_compiled_backends!(other) do
    raise ArgumentError,
          "config :emerge, compiled_backends: ... must be a list of backend atoms, got: #{inspect(other)}"
  end

  @doc false
  def compiled_backends_to_rustler_features(backends) do
    backends
    |> normalize_compiled_backends!()
    |> Enum.map(&Atom.to_string/1)
  end

  @doc false
  def load_native_runtime?(
        env \\ System.get_env(),
        compiled_backends \\ compiled_backends(),
        mix_env \\ Mix.env()
      )
      when is_map(env) and is_list(compiled_backends) do
    compiled_backends = normalize_compiled_backends!(compiled_backends)

    cond do
      Map.get(env, @load_macos_nif_env_key) in ["1", "true"] ->
        true

      mix_env == :test ->
        true

      host_darwin?(env) and compiled_backends == [:macos] ->
        false

      host_ios?(env) and compiled_backends == [:ios] ->
        false

      host_android?(env) and compiled_backends == [:android] ->
        false

      true ->
        true
    end
  end

  @doc false
  def precompiled_variants(env \\ System.get_env(), compiled_backends \\ compiled_backends())
      when is_map(env) and is_list(compiled_backends) do
    %{
      "x86_64-unknown-linux-gnu" => [
        drm: fn _config ->
          precompiled_variant?(env, compiled_backends, "x86_64-unknown-linux-gnu", :drm)
        end,
        drm_wayland: fn _config ->
          precompiled_variant?(env, compiled_backends, "x86_64-unknown-linux-gnu", :drm_wayland)
        end
      ],
      "aarch64-unknown-linux-gnu" => [
        drm: fn _config ->
          precompiled_variant?(env, compiled_backends, "aarch64-unknown-linux-gnu", :drm)
        end,
        drm_wayland: fn _config ->
          precompiled_variant?(env, compiled_backends, "aarch64-unknown-linux-gnu", :drm_wayland)
        end
      ]
    }
  end

  @doc false
  def precompiled_profile(env, compiled_backends, target)
      when is_map(env) and is_list(compiled_backends) and is_binary(target) do
    compiled_backends = normalize_compiled_backends!(compiled_backends)

    cond do
      compiled_backends == [:wayland] and target in @precompiled_targets ->
        {:ok, %{target: target, variant: nil, backends: compiled_backends}}

      target == "x86_64-unknown-linux-gnu" and compiled_backends == [:drm] ->
        {:ok, %{target: target, variant: :drm, backends: compiled_backends}}

      target == "x86_64-unknown-linux-gnu" and compiled_backends == [:wayland, :drm] ->
        {:ok, %{target: target, variant: :drm_wayland, backends: compiled_backends}}

      target == "aarch64-unknown-linux-gnu" and compiled_backends == [:drm] ->
        {:ok, %{target: target, variant: :drm, backends: compiled_backends}}

      target == "aarch64-unknown-linux-gnu" and compiled_backends == [:wayland, :drm] ->
        {:ok, %{target: target, variant: :drm_wayland, backends: compiled_backends}}

      true ->
        {:error, :unsupported_profile}
    end
  end

  @doc false
  def precompiled_source_url(env \\ System.get_env()) when is_map(env) do
    Map.get(env, @precompiled_source_url_env_key, @default_precompiled_source_url)
  end

  @doc false
  def macos_host_target(env \\ System.get_env()) when is_map(env) do
    case {Map.get(env, "TARGET_ARCH"), Map.get(env, "TARGET_OS")} do
      {arch, os} when is_binary(arch) and arch != "" and os == "darwin" ->
        macos_target_from_arch(arch)

      _ ->
        current_target_system()
        |> Map.get(:arch)
        |> macos_target_from_arch()
    end
  end

  @doc false
  def macos_host_archive_name(target, version \\ @version)
      when is_binary(target) and is_binary(version) do
    "macos_host-v#{version}-#{target}.tar.gz"
  end

  @doc false
  def macos_host_checksum_name(target, version \\ @version)
      when is_binary(target) and is_binary(version) do
    "#{macos_host_archive_name(target, version)}.sha256"
  end

  @doc false
  def macos_host_download_url(target, env \\ System.get_env(), version \\ @version)
      when is_binary(target) and is_map(env) and is_binary(version) do
    release_asset_url(macos_host_archive_name(target, version), version, env)
  end

  @doc false
  def macos_host_checksum_url(target, env \\ System.get_env(), version \\ @version)
      when is_binary(target) and is_map(env) and is_binary(version) do
    release_asset_url(macos_host_checksum_name(target, version), version, env)
  end

  @doc false
  def macos_host_cache_dir(target, version \\ @version, env \\ System.get_env())
      when is_binary(target) and is_binary(version) and is_map(env) do
    base =
      case Map.get(env, @macos_host_cache_dir_env_key) do
        path when is_binary(path) and path != "" -> path
        _ -> user_cache_dir()
      end

    Path.join([base, "emerge_skia", "macos_host", version, target])
  end

  @doc false
  def precompiled_tar_gz_url(file_name), do: precompiled_tar_gz_url(file_name, System.get_env())

  @doc false
  def precompiled_tar_gz_url(file_name, env) when is_binary(file_name) and is_map(env) do
    release_asset_url(file_name, @version, env)
  end

  @doc false
  def force_precompiled_build?(opts \\ []) when is_list(opts) do
    env = Keyword.get(opts, :env, System.get_env())
    checksum_path = Keyword.fetch!(opts, :checksum_path)
    compiled_backends = Keyword.get(opts, :compiled_backends, compiled_backends())
    targets = Keyword.get(opts, :targets, @precompiled_targets)
    nif_versions = Keyword.get(opts, :nif_versions, @precompiled_nif_versions)
    target_resolver = Keyword.get(opts, :target_resolver, &default_precompiled_target_resolver/2)

    force_build_requested?(env) ||
      not File.exists?(checksum_path) ||
      unsupported_precompiled_profile?(
        env,
        compiled_backends,
        target_resolver,
        targets,
        nif_versions
      )
  end

  @doc false
  def default_runtime_backend(backends) do
    backends
    |> normalize_compiled_backends!()
    |> case do
      [] -> :wayland
      normalized -> Enum.find(@valid_backends, &(&1 in normalized)) || :wayland
    end
  end

  defp nerves_compiler?(nil), do: false

  defp nerves_compiler?(compiler) do
    case compiler_prefix(compiler) do
      "armv6-nerves-linux-gnueabihf" -> true
      "armv7-nerves-linux-gnueabihf" -> true
      "aarch64-nerves-linux-gnu" -> true
      "x86_64-nerves-linux-musl" -> true
      _other -> false
    end
  end

  defp mix_target?(env) do
    case Map.get(env, "MIX_TARGET") do
      target when is_binary(target) and target not in ["", "host"] -> true
      _ -> false
    end
  end

  defp host_darwin?(env) when is_map(env) do
    case Map.get(env, "TARGET_OS") do
      os when is_binary(os) and os != "" -> os == "darwin"
      _ -> :os.type() == {:unix, :darwin}
    end
  end

  defp default_precompiled_target_resolver(targets, nif_versions) do
    RustlerPrecompiled.target(
      %{
        os_type: :os.type(),
        target_system: current_target_system(),
        word_size: :erlang.system_info(:wordsize),
        nif_version: current_compatible_nif_version(nif_versions)
      },
      targets,
      nif_versions
    )
  end

  defp current_compatible_nif_version(nif_versions) do
    current_nif_version = :erlang.system_info(:nif_version) |> List.to_string()

    case RustlerPrecompiled.find_compatible_nif_version(current_nif_version, nif_versions) do
      {:ok, version} -> version
      :error -> current_nif_version
    end
  end

  defp current_target_system do
    :erlang.system_info(:system_architecture)
    |> List.to_string()
    |> String.split("-")
    |> system_arch_parts_to_map()
    |> RustlerPrecompiled.maybe_override_with_env_vars()
  end

  defp system_arch_parts_to_map([arch, vendor, os, abi]),
    do: %{arch: arch, vendor: vendor, os: os, abi: abi}

  defp system_arch_parts_to_map([arch, vendor, os]),
    do: %{arch: arch, vendor: vendor, os: os}

  defp system_arch_parts_to_map(_parts), do: %{}

  defp force_build_requested?(env) do
    Map.get(env, @force_precompiled_build_env_key) in ["1", "true"]
  end

  defp maybe_authenticated_direct_url(url, env) do
    case Map.get(env, @github_token_env_key) do
      token when is_binary(token) and token != "" ->
        {url,
         [
           {"Authorization", "Bearer #{token}"},
           {"Accept", "application/octet-stream"},
           {"User-Agent", "emerge-skia-precompiled"}
         ]}

      _ ->
        url
    end
  end

  defp release_asset_url(file_name, version, env)
       when is_binary(file_name) and is_binary(version) and is_map(env) do
    source_url = precompiled_source_url(env)
    direct_url = "#{source_url}/releases/download/v#{version}/#{file_name}"

    case github_release_asset_request(source_url, version, file_name, env) do
      {:ok, request} -> request
      :error -> maybe_authenticated_direct_url(direct_url, env)
    end
  end

  defp github_release_asset_request(source_url, version, file_name, env) do
    with token when is_binary(token) and token != "" <- Map.get(env, @github_token_env_key),
         {:ok, owner, repo} <- github_repo(source_url),
         {:ok, asset_url} <- github_release_asset_url(owner, repo, version, file_name, token) do
      {:ok,
       {asset_url,
        [
          {"Authorization", "Bearer #{token}"},
          {"Accept", "application/octet-stream"},
          {"X-GitHub-Api-Version", "2022-11-28"},
          {"User-Agent", "emerge-skia-precompiled"}
        ]}}
    else
      _ -> :error
    end
  end

  defp github_repo(source_url) when is_binary(source_url) do
    case Regex.run(~r/^https:\/\/github\.com\/([^\/]+)\/([^\/]+?)(?:\.git)?\/?$/, source_url) do
      [_, owner, repo] -> {:ok, owner, repo}
      _ -> :error
    end
  end

  defp github_release_asset_url(owner, repo, version, file_name, token) do
    release_url = "https://api.github.com/repos/#{owner}/#{repo}/releases/tags/v#{version}"

    headers = [
      {~c"Authorization", ~c"Bearer " ++ String.to_charlist(token)},
      {~c"Accept", ~c"application/vnd.github+json"},
      {~c"X-GitHub-Api-Version", ~c"2022-11-28"},
      {~c"User-Agent", ~c"emerge-skia-precompiled"}
    ]

    :inets.start()
    :ssl.start()

    case :httpc.request(:get, {String.to_charlist(release_url), headers}, [],
           body_format: :binary
         ) do
      {:ok, {{_, 200, _}, _response_headers, body}} ->
        with {:ok, %{"assets" => assets}} <- Jason.decode(body),
             %{"url" => asset_url} <- Enum.find(assets, &(&1["name"] == file_name)) do
          {:ok, asset_url}
        else
          _ -> :error
        end

      _ ->
        :error
    end
  end

  defp unsupported_precompiled_profile?(
         env,
         compiled_backends,
         target_resolver,
         targets,
         nif_versions
       ) do
    with {:ok, nif_target} <- target_resolver.(targets, nif_versions),
         {:ok, target} <- target_from_nif_target(nif_target),
         {:ok, _profile} <- precompiled_profile(env, compiled_backends, target) do
      false
    else
      _ -> true
    end
  end

  defp target_from_nif_target(nif_target) when is_binary(nif_target) do
    case String.split(nif_target, "-", parts: 3) do
      ["nif", _nif_version, target] when target != "" -> {:ok, target}
      _ -> {:error, :invalid_nif_target}
    end
  end

  defp precompiled_variant?(env, compiled_backends, target, variant) do
    case precompiled_profile(env, compiled_backends, target) do
      {:ok, %{variant: ^variant}} -> true
      _ -> false
    end
  end

  defp macos_target_from_arch("aarch64"), do: "aarch64-apple-darwin"
  defp macos_target_from_arch("arm64"), do: "aarch64-apple-darwin"
  defp macos_target_from_arch("x86_64"), do: "x86_64-apple-darwin"
  defp macos_target_from_arch("amd64"), do: "x86_64-apple-darwin"

  defp macos_target_from_arch(other) do
    raise ArgumentError, "unsupported macOS target architecture: #{inspect(other)}"
  end

  defp user_cache_dir do
    :filename.basedir(:user_cache, "", %{os: :darwin})
    |> to_string()
  end

  defp compiler_prefix(nil), do: nil

  defp compiler_prefix(compiler) do
    compiler
    |> String.split(~r/\s+/, trim: true)
    |> List.first()
    |> case do
      nil ->
        nil

      path ->
        path
        |> Path.basename()
        |> String.split("-")
        |> Enum.drop(-1)
        |> Enum.join("-")
    end
  end

  defp value_present?(value) when is_binary(value), do: value != ""
  defp value_present?(_value), do: false
end
